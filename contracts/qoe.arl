archetype qoe_dao

//-----------------------------------------------------------------------------
// Config
//-----------------------------------------------------------------------------

variable init_topo_cid        : string   = "QmSAHEgh5NozH8ALJk11WP4SdKSC1pQwnrFJ7wadUkz1gS"  
variable init_weights_cid     : string   = "QmayScJpjfU2FBosY2EJYz81D5Cw5H7dd4RnfrNFhF7qXZ"
variable quorum               : rational = 4/5          
variable super_majority       : rational = 2/3      
variable fraction_of_members  : rational = 1/4
variable min_models           : nat      = 3


//-----------------------------------------------------------------------------
// Storage
//-----------------------------------------------------------------------------

variable current_round : nat = 1

asset members identified by addr {
  addr      : address;
  point     : nat = 1;
  withdrew  : nat = 1;
}

asset models identified by id {
  id            : bytes;
  topo_cid      : string;
  weights_cid   : string;
  nbsamples     : nat;
  owner         : address;
  round         : nat;
  accepted      : bool = false;
  accept_point  : nat;
  reject_point  : nat = 0;
  abstain_point : nat = 0;
  voters        : set<address>;
}

enum vote_value = 
| REJECT    //  0
| ACCEPT    //  1
| ABSTAIN   //  2

//-----------------------------------------------------------------------------
// Entrypoint
//-----------------------------------------------------------------------------

entry contribute(t_cid: string, w_cid: string, number_of_samples: nat) {
  fail if {
    f: models.select(the.topo_cid = t_cid and the.weights_cid = w_cid).count() > 0 with "MODEL_ALREADY_EXIST";
    f0: models.select(the.owner = source and the.round = current_round).count() > 0 with "CONTRIBUTE_ONCE_EACH_ROUND";
  }
  effect {
    if not members.contains(source) then members.add({addr = source});
    const model_id  = keccak(pack(t_cid + w_cid));
    models.add({
      id            = model_id;
      topo_cid      = t_cid;
      weights_cid   = w_cid;
      nbsamples     = number_of_samples;
      owner         = source;
      round         = current_round;
      accept_point  = members[source].point;
      voters        = [source] 
    });
  }
}

entry vote(model_id: bytes, value: vote_value) {
  require {
    r   : models[model_id].accepted = false or models[model_id].round = current_round otherwise "POLL_ENDED";
    r0  : members.contains(source) otherwise "ONLY_MEMBER_CAN_VOTE" 
  }
  fail if {
    f1  : models[model_id].voters.contains(source) with "VOTED_ADDRESS"
  }
  effect {
    const voter_point = members[source].point;
    models[model_id].voters.add(source);
    match value with
    | ACCEPT  ->  models[model_id].accept_point   += voter_point
    | REJECT  ->  models[model_id].reject_point   += voter_point
    | _       ->  models[model_id].abstain_point  += voter_point
    end;

    // if enough accept point then model is accepted
    if is_enough_accept_point(model_id) then models[model_id].accepted := true;

    // if enough accepted local models then finish round
    if is_enough_accepted_model() then finish_round()
  }
}

entry default() {}

entry withdraw(amount: nat) {
  require {
    r1: members.contains(source) otherwise "ONLY_MEMBER";
    r2: members[source].point >= members[source].withdrew + amount otherwise "NOT_ENOUGH_POINT";
  }
  effect {
    transfer (amount*0.25tz) to source;     // 1 point = 0.25 tez = 0.21$ = 5.000â‚«
    members[source].withdrew += amount;
  }
}

//-----------------------------------------------------------------------------
// Utils
//-----------------------------------------------------------------------------

function participation_rate_of(model_id: bytes) : rational {
  const participation : nat = models[model_id].accept_point + models[model_id].reject_point + models[model_id].abstain_point;
  const total         : nat = members.sum(point);
  return (participation / total)
}

function accept_rate_of(model_id: bytes) : rational {
  return (models[model_id].accept_point / (models[model_id].accept_point + models[model_id].reject_point))
}

function is_enough_accept_point(model_id: bytes) : bool {
  const p_rate = participation_rate_of(model_id);
  if p_rate < quorum then 
    return false
  else begin
    const a_rate = accept_rate_of(model_id);
    return (a_rate >= super_majority)
  end
}

function is_enough_accepted_model() : bool {
  const num_accepted_models = models.select(the.round = current_round and the.accepted = true).count();
  const num_required_models = max(min_models, ceil(members.count() * fraction_of_members));
  return (num_accepted_models >= num_required_models)
}

function finish_round() {
  const accepted_models = models.select(the.round = current_round and the.accepted = true);
  for model_id in accepted_models do
      const model_owner = models[model_id].owner;
      members[model_owner].point += 4;
  done;
  current_round += 1
}

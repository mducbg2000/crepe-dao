archetype qoe_dao

//-----------------------------------------------------------------------------
// Config
//-----------------------------------------------------------------------------

variable model_topo_cid      : string   = "QmSAHEgh5NozH8ALJk11WP4SdKSC1pQwnrFJ7wadUkz1gS"  
variable init_weights_cid    : string   = "QmayScJpjfU2FBosY2EJYz81D5Cw5H7dd4RnfrNFhF7qXZ"
variable quorum              : rational = 4/5          
variable super_majority      : rational = 2/3      
variable fraction_of_members : rational = 1/4
variable min_models          : nat      = 3
variable reward_point        : nat      = 3


//-----------------------------------------------------------------------------
// Storage
//-----------------------------------------------------------------------------

variable current_round : nat = 1

asset member identified by addr {
  addr  : address;
  point : nat = 1;
}

asset pool identified by id {
  id        : string;
  nbsamples : nat;
  owner     : address;
  round     : nat;
  accepted  : bool = false;
}

asset poll identified by model_id {
  model_id      : string;
  accept_point  : nat;
  reject_point  : nat = 0;
  abstain_point : nat = 0;
  voter         : set<address>;
}

enum vote_value = 
| REJECT    //  0
| ACCEPT    //  1
| ABSTAIN   //  2

//-----------------------------------------------------------------------------
// Entrypoint
//-----------------------------------------------------------------------------

entry add_local_model(model_cid: string, number_of_samples: nat) {
  fail if {
    f: pool.contains(model_cid) with "MODEL_ALREADY_EXIST";
    f0: pool.select(the.owner = source and the.round = current_round).count() > 0 with "ADDED_IN_CURRENT_ROUND";
  }
  effect {
    pool.add({
      id        = model_cid;
      nbsamples = number_of_samples;
      owner     = source;
      round     = current_round
    });

    if not member.contains(source) then member.add({addr = source});

    poll.add({
      model_id      = model_cid;
      accept_point  = member[source].point;
      voter         = [source] 
    });

    emit<NewLocalModel>({model_cid; number_of_samples; current_round ; now})
  }
}

entry finish_round() {
  called by self_address
  effect {
    const accepted_models = pool.select(the.round = current_round and the.accepted = true);

    for model_cid in accepted_models do
      const model_owner = pool[model_cid].owner;
      member[model_owner].point += reward_point;
    done;

    emit<FinishRound>({current_round ; now});
    
    current_round += 1
  }
}

entry vote(model_cid: string, value: vote_value) {
  require {
    r   : pool[model_cid].accepted = false or pool[model_cid].round = current_round otherwise "POLL_ENDED";
    r0  : member.contains(source) otherwise "ONLY_MEMBER_CAN_VOTE" 
  }
  fail if {
    f1  : poll[model_cid].voter.contains(source) with "VOTED_ADDRESS"
  }
  effect {
    const voter_point = member[source].point;
    poll[model_cid].voter.add(source);
    match value with
    | ACCEPT  ->  poll[model_cid].accept_point   += voter_point
    | REJECT  ->  poll[model_cid].reject_point   += voter_point
    | _       ->  poll[model_cid].abstain_point  += voter_point
    end;
    if is_accepted(model_cid) then pool[model_cid].accepted := true;
    // if enough accepted local models, finish round
    const num_accepted_models = pool.select(the.round = current_round and the.accepted = true).count();
    const num_required_models = max(min_models, ceil(member.count() * fraction_of_members));
    if num_accepted_models >= num_required_models then transfer 0tz to entry self.finish_round();
  }
}

//-----------------------------------------------------------------------------
// Utils
//-----------------------------------------------------------------------------

function participation_rate(model_cid: string) : rational {
  const participation : nat = poll[model_cid].accept_point + poll[model_cid].reject_point + poll[model_cid].abstain_point;
  const total         : nat = member.sum(point);
  return (participation / total)
}

function accept_rate(model_cid: string) : rational {
  return (poll[model_cid].accept_point / (poll[model_cid].accept_point + poll[model_cid].reject_point))
}

function is_accepted(model_cid: string) : bool {
  const p_rate = participation_rate(model_cid);
  if p_rate < quorum then 
    return false
  else begin
    const a_rate = accept_rate(model_cid);
    return (a_rate >= super_majority)
  end
}

//-----------------------------------------------------------------------------
// Events
//-----------------------------------------------------------------------------

event NewLocalModel {
  cid         : string;
  num_samples : nat;
  round_id    : nat;
  added_time  : date;
}

event FinishRound {
  last_round    : nat;
  finished_time : date;
}